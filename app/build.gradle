plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
}
def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()
localProperties.load(new FileInputStream(localPropertiesFile))

android {
    namespace 'com.odogwudev.example.cinephilescompanion'
    compileSdk 33

    defaultConfig {
        applicationId "com.odogwudev.example.cinephilescompanion"
        minSdk 30
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            keyAlias = "Cinephile"
            keyPassword = "android"
            storePassword = "android"
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "BASE_URL", "\"https://api.themoviedb.org/3/\"")
            buildConfigField "String", "API_KEY", localProperties['apiKey']
            buildConfigField("String", "CLIENT_ID", "\"mova-mobile-client\"")
        }

        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true

            buildConfigField("String", "BASE_URL", "\"https://api.themoviedb.org/3/\"")
            buildConfigField "String", "API_KEY", localProperties['apiKey']
            buildConfigField("String", "CLIENT_ID", "\"mova-mobile-client\"")

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.androidx.composeCompiler.get()
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    // Kotlin
    implementation(libs.kotlinx.coroutines)

    // AndroidX
    implementation(libs.bundles.androidx.core)
    implementation(libs.andoridx.activityCompose)
    implementation(libs.androidx.appcompat)
    implementation(libs.bundles.androidx.lifecycle)

    implementation(libs.androidx.navigationCompose)
    implementation(libs.bundles.androidx.compose)
    implementation(libs.androidx.preferenceDataStore)
    implementation(libs.androidx.profileInstaller)

    // Google
    implementation(libs.google.android.material)

    // Koin Libraries
    implementation(libs.koin.core)
    implementation(libs.koin.compose)

    // Coil Image
    implementation(libs.bundles.coil)

    // Voyager
    implementation(libs.bundles.voyager)

    // State Machine
    implementation(libs.statemachine)
    implementation project(path: ':features:networking:network-implementation')

    // Test libraries
    testImplementation(libs.test.junit4)
    testImplementation(libs.bundles.test.junit5)
    testRuntimeOnly(libs.test.junit5.jupiterEngine)
    // Kotlin
    testImplementation(libs.test.kotlin.coroutines)
    // Robolectric
    testImplementation(libs.bundles.test.robolectric)
    testRuntimeOnly(libs.test.junit5.vintageEngine)
    // Other libraries
    testImplementation(libs.test.mockitoKotlin)
    testImplementation(libs.test.jraska.livedata)
    testImplementation(libs.test.square.okhttp.webserver)

    // Android Test libraries
    androidTestImplementation(libs.bundles.test.android)
    // Kotlin
    androidTestImplementation(libs.test.kotlin.coroutines)

    // Beagle
    debugImplementation(libs.beagle.uiDrawer)
    debugImplementation(libs.beagle.okHttp)
    releaseImplementation(libs.beagle.noop)
    releaseImplementation(libs.beagle.okHttpNoop)

    // Leakcanary
    debugImplementation(libs.leakcanary)
}